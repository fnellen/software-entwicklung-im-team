plugins {
    id 'com.github.spotbugs' version '5.0.6' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'

    group = 'de.hhu.propra'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}"
    }

    spotbugs {
        toolVersion = "4.6.0"
        effort = "max"
    }

    tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
        reports {
            xml.required = false
            html.required = true
        }
    }

    checkstyle {
        toolVersion = "8.41.1" // 8.42 breaks compat, see https://github.com/checkstyle/checkstyle/issues/7417
        ignoreFailures = false
        maxWarnings = 0
    }

    println(checkstyle.toolVersion)

    tasks.withType(Checkstyle) {
        reports {
            xml.required = false
            html.required = true
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}
